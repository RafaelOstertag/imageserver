plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.21'
    id 'org.owasp.dependencycheck' version '6.0.4'
    id "com.github.ben-manes.versions" version "0.28.0"
    id "org.sonarqube" version "2.7.1"
    id 'maven-publish'
    id 'jacoco'
}

group 'ch.guengel'
version '2.0.10-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    def ktorVersion = "1.5.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "io.ktor:ktor-server-netty:$ktorVersion"
    implementation "org.slf4j:slf4j-api:1.7.30"
    implementation "ch.qos.logback:logback-classic:1.2.3"
    implementation "biz.paluch.logging:logstash-gelf:1.14.1"
    // Keep this in sync with ktor-metrics-micrometer
    implementation 'io.micrometer:micrometer-registry-prometheus:1.6.2'
    implementation "io.ktor:ktor-metrics-micrometer:$ktorVersion"
    implementation "io.ktor:ktor-gson:$ktorVersion"


    testImplementation "org.junit.jupiter:junit-jupiter:5.6.0"
    testImplementation "com.willowtreeapps.assertk:assertk-jvm:0.20"
}

test {
    useJUnitPlatform()
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

jar {
    from sourceSets.main.output

    manifest {
        attributes 'Main-Class': 'ch.guengel.imageserver.MainKt'
    }

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'imageserver'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Image Server'
                description = 'Server sending random images'
            }
        }
    }
    repositories {
        maven {
            credentials {
                username publishUser
                password publishPassword
            }
            url = 'https://colossus.kruemel.home/nexus/repository/imageserver/'
        }
    }
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.enabled true
    }
}

dependencyCheck {
    formats = ['HTML', 'XML']
    analyzers {
        assemblyEnabled = false
    }
    suppressionFile = 'dependency-check-suppression.xml'
}


plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'org.owasp.dependencycheck' version '5.3.2'
    id "com.github.ben-manes.versions" version "0.28.0"
    id 'maven-publish'
}

group 'ch.guengel'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "io.ktor:ktor-server-netty:1.3.1"
    implementation "org.slf4j:slf4j-api:1.7.30"
    implementation "ch.qos.logback:logback-classic:1.2.3"

    testImplementation "org.junit.jupiter:junit-jupiter:5.6.0"
    testImplementation "com.willowtreeapps.assertk:assertk-jvm:0.20"
}

test {
    useJUnitPlatform()
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

task uberJar(type: Jar) {
    from sourceSets.main.output

    manifest {
        attributes 'Main-Class': 'ch.guengel.imageserver.MainKt'
    }

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

assemble {
    dependsOn(uberJar)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'imageserver'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Image Server'
                description = 'Server sending random images'
            }
        }
    }
    repositories {
        maven {
            credentials {
                username publishUser
                password publishPassword
            }
            url = 'https://colossus.kruemel.home/nexus/repository/imageserver/'
        }
    }


}

